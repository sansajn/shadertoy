# dependencies:
#    libglfw3-dev (3.2.1, ubuntu 18.04)
#    libgles2-mesa-dev (18.0.5, ubuntu 18.04)
#    libglm-dev (0.9.9, ubuntu 18.04)
#    libboost-all-dev (1.65.1, ubuntu 18.04)
#    libmagick++-dev (6.8.9.9, ubuntu 18.04)

def create_build_environment():
	env = Environment(
		CCFLAGS=['--std=c++17', '-O0', '-g', '-Wall', '-Wno-sign-compare'],
		CPPPATH=['.', 'libs'],
		CPPDEFINES=['GLFW_INCLUDE_ES2', 'GLM_ENABLE_EXPERIMENTAL', 'GLM_FORCE_RADIANS',
			'USE_GLFW3', 'USE_IMAGICK',
			'HAVE_X11'  # sofd
		],
		LIBS=['boost_filesystem', 'boost_system', 'boost_program_options'])

	env.ParseConfig('pkg-config --cflags --libs glesv2 x11 glfw3 Magick++ freetype2')

	return env


env = create_build_environment()

gles2_objs = [
	'libs/gles2/' + f for f in [
		'mesh_gles2.cpp',
		'program_gles2.cpp',
		'texture_gles2.cpp',
		'model_gles2.cpp',
		'property.cpp',
		'texture_loader_gles2.cpp',
		'ui/label_gles2.cpp',
		'ui/text.cpp',
		'ui/texture_view.cpp',
		'application.cpp'
	]
]

gl_objs = [
	env.Object([
		'libs/gl/window.cpp',
		'libs/gl/glfw3_user_input.cpp',
		'libs/gl/glfw3_window.cpp'])
]

sofd = env.Object(['libs/sofd/libsofd.c'])
file_view = env.Object(Glob('libs/file_view/*.cpp'))

env.Program([
	'shadertoy.cpp',
	'app.cpp',
	'shadertoy_program.cpp',
	'file_chooser_dialog.cpp',
	'clock.cpp',
	'key_press_event.cpp',
	'utility.cpp',
	'project_file.cpp',
	gles2_objs, gl_objs, sofd, file_view])

env.Program(['test_sofd.cpp', sofd])
